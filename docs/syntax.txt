program = {function} ? EOFToken ?;

function = signature {construct} end;
signature = identifier '<' [parameters] '>' EOL;

arguments = value {',' value};
parameters = identifier {',' identifier};

construct = statement EOL | if_block;

if_block = 'if' conditional_block {'elif' conditional_block} ['else' EOL {construct} end];
conditional_block = value EOL {construct} end;

statement = return | assignment | mutation | call;

assignment = identifier '=' value;
mutation = identifier '=>' value;
call = identifier '<' [arguments] '>';
return = '~' value;

value = '(' value ')' | number | identifier | call | operation;

operation = value operator value;
operator = '+' | '-' | '*' | '/' | '%' | '==' | '!=' | '<' | '>' | '<=' | '>=';

end = 'end' EOL;

identifier = ? IdentifierToken ?;
number = ['-'] ? NumberToken ?;
EOL = ? EOLToken ?;