program = {function} ? EOFToken ?;

function = signature {construct} end;
signature = identifier '<' [parameters] '>' EOL;

arguments = value {',' value};
parameters = identifier {',' identifier};

construct = if_block | statement EOL;

if_block = 'if' conditional_block {'elif' conditional_block} ['else' EOL {construct} end];
conditional_block = value EOL {construct} end;

statement = return | assignment | mutation | call;

assignment = identifier '=' value;
mutation = identifier '=>' value;
call = identifier '<' [arguments] '>';
return = '~' [value];

value = '(' value ')' | call | operation | number | identifier;

operation = term operator value;
operator = '+' | '-' | '*' | '/' | '%' | '==' | '!=' | '<' | '>' | '<=' | '>=';
term = '(' value ')' | call | number | identifier;

end = 'end' EOL;

identifier = ? IdentifierToken ?;
number = ['-'] ? NumberToken ?;
EOL = ? EOLToken ?;