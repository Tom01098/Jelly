program = {function} ? EOFToken ?;

function = signature constructs end;
signature = identifier '<' [parameters] '>' EOL;

arguments = value {',' value};
parameters = identifier {',' identifier};

constructs = {construct};
construct = if_block | loop_block | statement EOL;

if_block = 'if' conditional_block {'elif' conditional_block} ['else' EOL constructs end];
loop_block = 'loop' conditional_block;
conditional_block = value EOL constructs end;

statement = return | assignment | mutation | call;

assignment = identifier '=' value;
mutation = identifier '=>' value;
call = identifier '<' [arguments] '>';
return = '~' [value];

value = term [operator value];
operator = '+' | '-' | '*' | '/' | '%' | '==' | '!=' | '<<' | '>>' | '<=' | '>=';
term = '(' value ')' | absolute | not | negative | call | number | identifier;

identifier = ? IdentifierToken ?;
number = ? NumberToken ?;
not = '!' term;
negative = '-' term;
absolute = '|' value '|'

EOL = ? EOLToken ?;
end = 'end' EOL;